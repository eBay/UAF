buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'commons-codec:commons-codec:1.9'
    compile 'org.bouncycastle:bcprov-jdk15on:1.51'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    testCompile 'junit:junit:4.12'
}

def releaseVersionName = "1.0.4"

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['BintrayPublication']
    pkg {
        repo = 'UAF-Java'
        name = 'UAF-Java'
        licenses = ['Apache-2.0']
        labels = ['FIDO', 'UAF']
        vcsUrl = 'https://github.com/neXenio/UAF.git'
        websiteUrl = 'https://github.com/neXenio/UAF/'
        githubRepo = 'neXenio/UAF/'
        githubReleaseNotesFile = 'README.md'
        version {
            name = "${releaseVersionName}"
            desc = "UAF Java ${releaseVersionName}"
            released = new Date()
            vcsTag = "${releaseVersionName}"
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('mavenCentralUser') ? project.property('mavenCentralUser') : System.getenv('MAVEN_CENTRAL_USER')
                password = project.hasProperty('mavenCentralPassword') ? project.property('mavenCentralPassword') : System.getenv('MAVEN_CENTRAL_PASSWORD')
                close = '1'
            }
        }
    }
    filesSpec {
        from 'build/libs'
        into 'libs/'
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'gradle-dev'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.groupId}"
            artifactId "${artifactId}"

            from components.java
            artifact sourcesJar
        }
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.nexenio.fido'
            artifactId 'uaf-core'
            version "${releaseVersionName}"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java implementation of the UAF protocol')
                root.appendNode('name', 'UAF Java')
                root.appendNode('url', 'https://github.com/neXenio/UAF/')
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// configure release plugin. See https://github.com/researchgate/gradle-release#configuration
release {
    git {
        requireBranch = 'master'
    }
}

// deploy artifacts when releasing
afterReleaseBuild.dependsOn artifactoryPublish
